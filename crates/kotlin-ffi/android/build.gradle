buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.1'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.10'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'

android {
    namespace "com.reown.yttrium"

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
            java.srcDirs = ['src/main/kotlin']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

     packagingOptions {
        doNotStrip "*/libuniffi_yttrium.so"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }
}

publishing {
    publications {
        release(MavenPublication) {
            // Use the AAR file generated by the build
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            // Customize POM details
            groupId = 'com.github.reown-com'
            artifactId = 'yttrium'
            version = '0.2.49'

            pom {
                name.set("Yttrium")
                description.set("An Android library for Yttrium.")
                url.set("https://github.com/reown-com/Yttrium")

                 scm {
                        connection.set("scm:git:git://github.com:reown-com/yttrium.git")
                        developerConnection.set("scm:git:ssh://github.com:reown-com/yttrium.git")
                        url.set("https://github.com/reown-com/yttrium")
                    }
            }
        }
    }
}

tasks.named("publishReleasePublicationToMavenLocal") {
    dependsOn("assembleRelease")
}